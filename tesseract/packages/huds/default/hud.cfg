hudfont wheaton

// clock
hudtextheight $clockh
hudalign top center
hudpos $clockx $clocky
hudsize $clockw $clockh
hudcolor 0x202020 100
hudrectangle
hudcolor 0xffffff
hudtext (clock (clocksecs))

// speed
if $showspeed [
   hudalign bottom center
   hudtextheight @speedh
   hudpos @speedx @speedy
   hudcolor 0x000000 100
   hudsize @speedw @speedh
   hudrectangle
   hudcolor 0xffffff 100
   hudsize (*f @speedw (divf (hud:speed) 500)) @speedh
   hudrectangle
   hudcolor 0xffffff
   hudtext (hud:speed)
] []

// weaponlist
hudalign bottom left
cx = $wlistx
loop i 7 [
    hasammo = (+ (hud:ammo $i) (hud:magazine $i))
    maxammo = (+ (hud:capacity $i) (hud:maxammo $i))
    if $hasammo [
        hudpos $cx $wlisty
        hudcolor (at $guncolors $i) 50
        hudsize $wlistw $wlisth
        hudrectangle
        ammo = (divf $hasammo $maxammo)
        hudalpha 255
        hudsize $wlistw (*f $wlisth $ammo)
        hudrectangle
        cx = (+ $cx $wlistw)
    ] []
]

hudalign center center
hudpos $bcastx $bcasty
hudtextheight $bcasth
hudcolor 0xffffff
hudtext (hud:broadcast)

// ammo
hudtextheight $ammoh
hudalign bottom center
hudpos $ammox $ammoy
hudsize $ammow $ammoh
hudcolor (at $guncolors (hud:gun))
gun = $hud:gun
if $gun [
   if (hud:capacity $gun) [
       hudtext (concatword (hud:magazine $gun) [\] (hud:ammo $gun))
   ] [
       hudtext (hud:ammo $gun)
   ]
] []

// health + armor + overall stack
hudtextheight $stackh
hudalign top center
hudpos $stackx $stacky
hudsize (* 2 $stackw) $stackh
hudcolor (at $armourcolors (hud:armourtype)) 100
hudrectangle

hudsize 1 $stackh
hudcolor 0xffffff 100
hudrectangle

hudcolor 0xffffff

hudalign top left
hudpos (- $stackx $stackw) $stacky
hudtext (hud:health)

hudalign top right
hudpos (+ $stackx $stackw) $stacky
hudtext (hud:armour)

hudalign bottom center
hudpos $stackx $stacky
hudsize (* 2 $stackw) (* 2 $stackh)
hudcolor 0xffffff
hudtextheight (* 2 $stackh)
hudtext (hud:survivable)

// keys
if $showkeys [
    hudalign bottom left
    hudtextheight @keysize

    keyposy = $keystarty
    keyposx = (- @xmid @lkeyskip)

    keyposy = (- $keyposy @keysize)
    keyposx = (- $keyposx (* 2 @keysize))
    hudpos $keyposx $keyposy
    if (< 0 (hud:strafe)) keydown keyup
    hudtext @lkey

    keyposx = (+ @lkeyskip $keyposx)
    hudpos $keyposx $keyposy
    if (> 0 (hud:move)) keydown keyup
    hudtext @bkey

    keyposx = (+ @bkeyskip $keyposx)
    hudpos $keyposx $keyposy
    if (> 0 (hud:strafe)) keydown keyup
    hudtext @rkey

    keyposx = (- $keyposx @bkeyskip)
    keyposy = (- $keyposy @keysize)
    hudpos $keyposx $keyposy
    if (< 0 (hud:move)) keydown keyup
    hudtext @fkey

    keyposy = (+ (* 2 @keysize) $keyposy)
    hudpos $keyposx $keyposy
    if (hud:jumping) keydown keyup
    hudtext @jkey
] []