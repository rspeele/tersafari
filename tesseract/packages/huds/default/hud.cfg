//hud_font wheaton

// clock
hud_textheight $clockh
hud_align top center
hud_pos $xmid 0
hud_size $clockw $clockh
hud_color 0x202020 100
hud_img
hud_color 0xffffff
hud_text (clock (clocksecs))

// speed
if $showspeed [
   hud_textheight @speedh
   hud_pos @speedx @speedy
   hud_text (hudp_speed)
] []

// weaponlist
hud_align bottom left
cx = $wlistx
loop i 7 [
    if (hudp_ammo $i) [
        hud_pos $cx $wlisty
        hud_color (at $guncolors $i) 50
        hud_size $wlistw $wlisth
        hud_img
        ammo = (divf (hudp_ammo $i) (at $gunmaxammos $i))
        hud_alpha 255
        hud_size $wlistw (*f $wlisth $ammo)
        hud_img
        cx = (+ $cx $wlistw)
    ] []
]

// ammo
hud_textheight $ammoh
hud_align bottom center
hud_pos $ammox $ammoy
hud_size $ammow $ammoh
hud_color (at $guncolors (hudp_gun)) 100
hud_img
hud_color 0xffffff
if (hudp_gun) [
    hud_text (hudp_ammo (hudp_gun))
] []

// health + armor + overall stack
hud_textheight $stackh
hud_align top center
hud_pos $stackx $stacky
hud_size (* 2 $stackw) $stackh
hud_color (at $armourcolors (hudp_armourtype)) 100
hud_img

hud_size 1 $stackh
hud_color 0xffffff 100
hud_img

hud_color 0xffffff

hud_align top left
hud_pos (- $stackx $stackw) $stacky
hud_text (hudp_health)

hud_align top right
hud_pos (+ $stackx $stackw) $stacky
hud_text (hudp_armour)

hud_align bottom center
hud_pos $stackx $stacky
hud_size (* 2 $stackw) (* 2 $stackh)
afac = (at $armourfacs (hudp_armourtype))
if (< (* (- $afac 1) (hudp_health)) (hudp_armour)) [
    survivable = (* $afac (hudp_health))
] [
    survivable = (+ (hudp_health) (hudp_armour))
]
hud_color (at $scolors (div $survivable 80)) 100
hud_img
hud_color 0xffffff
hud_textheight (* 2 $stackh)
hud_text $survivable

// keys
if $showkeys [
    hud_align bottom left
    hud_textheight @keysize
    
    keyposy = (hud_ypct 75)
    keyposx = (- @xmid (*f @keysize 1.33))

    keyposx = (+ @keysize $keyposx)
    hud_pos $keyposx $keyposy
    if (hudp_attacking) keydown keyup
    hud_text +
    
    keyposx = (+ @keysize $keyposx)
    hud_pos $keyposx $keyposy
    if (hudp_jumping) keydown keyup
    hud_text ^
    
    keyposy = (- $keyposy @keysize)
    keyposx = (- $keyposx (* 2 @keysize))
    hud_pos $keyposx $keyposy
    if (< 0 (hudp_strafe)) keydown keyup
    hud_text @lkey
    
    keyposx = (+ @keysize $keyposx)
    hud_pos $keyposx $keyposy
    if (> 0 (hudp_move)) keydown keyup
    hud_text @bkey
    
    keyposx = (+ @keysize $keyposx)
    hud_pos $keyposx $keyposy
    if (> 0 (hudp_strafe)) keydown keyup
    hud_text @rkey
    
    keyposx = (- $keyposx @keysize)
    keyposy = (- $keyposy @keysize)
    hud_pos $keyposx $keyposy
    if (< 0 (hudp_move)) keydown keyup
    hud_text @fkey
] []